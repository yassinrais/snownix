defmodule SnownixWeb.UserConfirmationControllerTest do
  use SnownixWeb.ConnCase, async: true

  alias Snownix.Accounts
  alias Snownix.Repo
  import Snownix.AccountsFixtures

  setup do
    %{user: user_fixture()}
  end

  describe "GET /account/confirm" do
    test "renders the resend confirmation page", %{conn: conn} do
      conn = get(conn, Routes.auth_reconfirm_path(conn, :reconfirm))
      response = html_response(conn, 200)
      assert response =~ "Resend confirmation instructions</h1>"
    end
  end

  describe "POST /account/confirm" do
    @tag :capture_log
    test "sends a new confirmation token", %{conn: conn, user: user} do
      conn =
        post(conn, Routes.user_confirmation_path(conn, :create), %{
          "user" => %{"email" => user.email}
        })

      assert redirected_to(conn) == "/auth/login"
      assert get_flash(conn, :info) =~ "If your email is in our system"
      assert Repo.get_by!(Accounts.UserToken, user_id: user.id).context == "confirm"
    end

    test "does not send confirmation token if User is confirmed", %{conn: conn, user: user} do
      Repo.update!(Accounts.User.confirm_changeset(user))

      conn =
        post(conn, Routes.user_confirmation_path(conn, :create), %{
          "user" => %{"email" => user.email}
        })

      assert redirected_to(conn) == "/auth/login"
      assert get_flash(conn, :info) =~ "If your email is in our system"
      refute Repo.get_by(Accounts.UserToken, user_id: user.id)
    end

    test "does not send confirmation token if email is invalid", %{conn: conn} do
      conn =
        post(conn, Routes.user_confirmation_path(conn, :create), %{
          "user" => %{"email" => "unknown@example.com"}
        })

      assert redirected_to(conn) == "/auth/login"
      assert get_flash(conn, :info) =~ "If your email is in our system"
      assert Repo.all(Accounts.UserToken) == []
    end
  end

  describe "GET /account/confirm/:token" do
    test "renders the confirmation page", %{conn: conn} do
      conn = get(conn, Routes.auth_confirm_path(conn, :confirm, "some-token"))
      response = html_response(conn, 200)
      assert response =~ "Confirm account</h1>"

      form_action = Routes.user_confirmation_path(conn, :update, "some-token")
      assert response =~ "action=\"#{form_action}\""
    end
  end

  describe "POST /account/confirm/:token" do
    test "confirms the given token once", %{conn: conn, user: user} do
      token =
        extract_user_token(fn url ->
          Accounts.deliver_user_confirmation_instructions(user, url)
        end)

      conn =
        post(
          conn,
          Routes.user_confirmation_path(conn, :update, token),
          %{"user" => %{"email" => user.email}}
        )

      assert redirected_to(conn) == "/auth/login"
      assert get_flash(conn, :info) =~ "User confirmed successfully"
      assert Accounts.get_user!(user.id).confirmed_at
      refute get_session(conn, :user_token)
      assert Repo.all(Accounts.UserToken) == []

      # When not logged in
      conn =
        post(conn, Routes.user_confirmation_path(conn, :update, token), %{
          "user" => %{"email" => user.email}
        })

      assert redirected_to(conn) == "/auth/login"
      assert get_flash(conn, :error) =~ "User confirmation link is invalid or it has expired"

      # When logged in
      conn =
        build_conn()
        |> log_in_user(user)
        |> post(Routes.user_confirmation_path(conn, :update, token), %{
          "user" => %{"email" => user.email}
        })

      assert redirected_to(conn) == "/auth/login"
      refute get_flash(conn, :error)
    end

    test "does not confirm email with invalid token", %{conn: conn, user: user} do
      conn =
        post(conn, Routes.user_confirmation_path(conn, :update, "oops"), %{
          "token" => nil,
          "user" => %{"email" => nil}
        })

      assert redirected_to(conn) == "/auth/login"
      assert get_flash(conn, :error) =~ "User confirmation link is invalid or it has expired"
      refute Accounts.get_user!(user.id).confirmed_at
    end

    test "does not confirm email with invalid email", %{conn: conn, user: user} do
      token =
        extract_user_token(fn url ->
          Accounts.deliver_user_confirmation_instructions(user, url)
        end)

      conn =
        post(conn, Routes.user_confirmation_path(conn, :update, token), %{
          "token" => token,
          "user" => %{"email" => "invalid-email@example.com"}
        })

      assert redirected_to(conn) == "/auth/login"
      assert get_flash(conn, :error) =~ "User confirmation link is invalid or it has expired"
      refute Accounts.get_user!(user.id).confirmed_at

      # Email nil
      conn =
        post(conn, Routes.user_confirmation_path(conn, :update, token), %{
          "token" => token,
          "user" => %{"email" => nil}
        })

      assert redirected_to(conn) == "/auth/login"
      assert get_flash(conn, :error) =~ "User confirmation link is invalid or it has expired"
    end
  end
end
